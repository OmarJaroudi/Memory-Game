title	"Memory_Game"	
list	p=16f84A
include "p16f84A.inc"


		c1		EQU	d'12'
		COUNT1	EQU	d'13'
		COUNT2	EQU	d'14'
		SCENE	EQU	d'15'		
		;"Scene" flag is used to indicate which scene we are in
		;there are 4 scenes (Main menu, Mode1,Mode2,Mode3)
		;so we can use 4 bits (1000 = Main menu,0100 = Mode1 etc.)
		;when any of the 4 bits is set to 1, the others are 0

		AC		EQU	d'16'	;indicates address of LCD
		tempA1	EQU	d'17'
		tempA2	EQU	d'18'
		tempB1	EQU	d'19'
		tempB2	EQU	d'20'
		COUNTER	EQU	d'21'
		IsOpen	EQU	d'22'	;indicates if a card is already open or not
		
		;LETTERS
		L1		EQU	d'23'
		L2		EQU	d'24'
		L3		EQU	d'25'
		L4		EQU	d'26'
		L5		EQU	d'27'
		L6		EQU	d'28'
		L7		EQU	d'29'
		L8		EQU	d'30'
		L9		EQU	d'31'
		L10		EQU	d'32'
		L11		EQU	d'33'
		L12		EQU	d'34'

		;Flags to tell whether each letter is currently open or not
		S1		EQU	d'35'
		S2		EQU	d'36'
		S3		EQU	d'37'
		S4		EQU	d'38'
		S5		EQU	d'39'
		S6		EQU	d'40'
		S7		EQU	d'41'
		S8		EQU	d'42'
		S9		EQU	d'43'
		S10		EQU	d'44'
		S11		EQU	d'45'
		S12		EQU	d'46'

		RECENT	EQU	d'47'
		RECENT@	EQU	d'48'
		TEMP	EQU	d'49'
		MATCHES	EQU	d'50'
		NOMATCH EQU d'51'
		ORG		0x0
		GOTO 	MAIN

		ORG		0x04
		BTFSC	INTCON,RBIF
		GOTO	RBINT
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;; OUTPUTS AND INPUTS ;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

MAIN	CLRF	MATCHES
		CLRF	NOMATCH
		MOVLW	b'0011'
		MOVWF	L1
		MOVWF	L10
		MOVLW	b'0001'
		MOVWF	L2
		MOVWF	L11
		MOVLW	b'0101'	
		MOVWF	L3
		MOVWF	L12
		MOVLW	b'0110'
		MOVWF	L4
		MOVWF	L9
		MOVLW	b'0100'
		MOVWF	L6
		MOVWF	L8
		MOVLW	b'0010'
		MOVWF	L5
		MOVWF	L7
		

		CLRF	SCENE
		CLRF	PORTB
		BSF		STATUS,RP0
		MOVLW	b'10000111'
		MOVWF	OPTION_REG
		CLRF	TRISA
		MOVLW	b'11110000'
		MOVWF	TRISB
		BCF		STATUS,RP0
		CLRF	INTCON
		BSF		INTCON,GIE
		BSF		INTCON,RBIE
		CLRF	PORTA

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;; INITIALIZATION SEQUENCE ;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

		;FUNCTION SET	
		CALL	DELAY2
		MOVLW	b'00010'
		MOVWF	PORTA
		CALL	ET
		;same data line
		CALL	ET
		MOVLW	b'01000'
		MOVWF	PORTA
		CALL	ET
		
		;DISPLAY ON/OFF
		MOVLW	b'00000'
		MOVWF	PORTA
		CALL	ET
		MOVLW	b'01100'
		MOVWF	PORTA
		CALL	ET

		;CLEAR DISPLAY
		CALL	Clear_Display

		;ENTRY MODE SET
		MOVLW	b'00000'
		MOVWF	PORTA
		CALL	ET
		MOVLW	b'00110'
		MOVWF	PORTA
		CALL	ET  


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;; IMPLMENETATION ;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

MEMORY_GAME	
		;SET ADDRESS
		MOVLW	b'01000'
		MOVWF	PORTA
		CALL	ET
		MOVLW	b'00010'
		MOVWF	PORTA
		CALL	ET

		;M
		CALL	Letter_M

		;E
		CALL	Letter_E

		;M
		CALL	Letter_M

		;O
		CALL	Letter_O

		CALL	Letter_R

		;Y
		MOVLW	b'10101'
		MOVWF	PORTA
		CALL	ET
		MOVLW	b'11001'
		MOVWF	PORTA
		CALL	ET

		;blank space
		CALL	Blank_Space

		;G
		MOVLW	b'10100'
		MOVWF	PORTA
		CALL	ET
		MOVLW	b'10111'
		MOVWF	PORTA
		CALL	ET

		CALL	Letter_A

		;M
		CALL	Letter_M

		;E
		CALL	Letter_E
		
		;delaying the welcome message for about 2.5 seconds
		MOVLW	d'50'
		MOVWF	c1
Label	CALL	DELAY2
		DECFSZ	c1,f
		GOTO Label
		

MAIN_MENU
		MOVLW	b'00000'
		MOVWF	PORTA
		CALL	ET
		MOVLW	b'01100'
		MOVWF	PORTA
		CALL	ET

		;CLEAR DISPLAY
		CALL	Clear_Display

		;SET ADDRESS
		CALL	Clear_Address
		
		MOVLW	b'1000'
		MOVWF	SCENE	; we are in the "main menu" scene
		MOVLW	d'6'
		MOVWF	AC

		;M
		CALL	Letter_M
		;O
		CALL	Letter_O
		;D
		MOVLW	b'10100'
		MOVWF	PORTA
		CALL	ET
		MOVLW	b'10100'
		MOVWF	PORTA
		CALL	ET
		;E
		CALL	Letter_E

		;blank space
		CALL	Blank_Space
		CALL	Blank_Space
		

		;1
		MOVLW	b'10011'
		MOVWF	PORTA
		CALL	ET
		MOVLW	b'10001'
		MOVWF	PORTA
		CALL	ET
	
		CALL	Blank_Space
		CALL	Blank_Space
		
		;2
		MOVLW	b'10011'
		MOVWF	PORTA
		CALL	ET
		MOVLW	b'10010'
		MOVWF	PORTA
		CALL	ET

		CALL	Blank_Space
		CALL	Blank_Space
		
		;3
		MOVLW	b'10011'
		MOVWF	PORTA
		CALL	ET
		MOVLW	b'10011'
		MOVWF	PORTA
		CALL	ET
		
		;SET ADDRESS
		MOVLW	b'01000'
		MOVWF	PORTA
		CALL	ET
		MOVLW	b'00101'
		MOVWF	PORTA
		CALL	ET

		;Asterisk cursor (*)
		CALL	Asterisk

InfLoop	BTFSC	SCENE,3
		GOTO	InfLoop
		BTFSC	SCENE,2
		GOTO	MODE1
		BTFSC	SCENE,1
		GOTO	MODE2
		BTFSC	SCENE,0
		GOTO	MODE3
		GOTO	InfLoop


MODE1	MOVLW	d'35'
		MOVWF	FSR
		MOVLW	d'13'
		MOVWF	COUNTER
S		DECFSZ	COUNTER,F
		GOTO	N
		GOTO 	cont
N		CLRF	INDF
		INCF	FSR
		GOTO 	S

cont	CLRF	MATCHES
		CLRF	NOMATCH
		CALL	INITIAL_CARDS
		MOVLW	b'01000'
		MOVWF	PORTA
		CALL	ET
		MOVLW	b'01000'
		MOVWF	PORTA
		CALL	ET
		
		CALL	Letter_S

		MOVLW	d'7'
		MOVWF	COUNTER
		
X		DECFSZ	COUNTER,F
		GOTO 	AX
		GOTO	AA
AX		MOVLW	b'11010'
		MOVWF	PORTA
		CALL	ET
		MOVLW	b'10011'
		MOVWF	PORTA
		CALL	ET
		GOTO	X

AA		CALL	Letter_W

		CALL	Clear_Address
		CLRF	AC
		MOVLW	d'9'
		MOVWF	tempA1
Mode1_Loop
		BTFSC	SCENE,3
		GOTO	MAIN_MENU
		GOTO	Mode1_Loop

MODE2
MODE3
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;; COMMONLY USED FUNCTIONS IN CODE ;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Clear_Display
		MOVLW	b'00000'
		MOVWF	PORTA
		CALL	ET
		MOVLW	b'00001'
		MOVWF	PORTA
		CALL	ET
		RETURN

Blank_Space
		MOVLW	b'11010'
		MOVWF	PORTA
		CALL	ET
		MOVLW	b'10000'
		MOVWF	PORTA
		CALL	ET
		RETURN

Asterisk
		MOVLW	b'10010'
		MOVWF	PORTA
		CALL	ET
		MOVLW	b'11010'
		MOVWF	PORTA
		CALL	ET
		RETURN

Letter_W
		MOVLW	b'10101'
		MOVWF	PORTA
		CALL	ET
		MOVLW	b'10111'
		MOVWF	PORTA
		CALL	ET
		RETURN

Letter_R
		MOVLW	b'10101'
		MOVWF	PORTA
		CALL	ET
		MOVLW	b'10010'
		MOVWF	PORTA
		CALL	ET
		RETURN

Letter_S	
		MOVLW	b'10101'
		MOVWF	PORTA
		CALL	ET
		MOVLW	b'10011'
		MOVWF	PORTA
		CALL	ET
		RETURN

Letter_O
		MOVLW	b'10100'
		MOVWF	PORTA
		CALL	ET
		MOVLW	b'11111'
		MOVWF	PORTA
		CALL	ET
		RETURN

Letter_M 
		MOVLW	b'10100'
		MOVWF	PORTA
		CALL	ET
		MOVLW	b'11101'
		MOVWF	PORTA
		CALL	ET
		RETURN

Letter_E
		MOVLW	b'10100'
		MOVWF	PORTA
		CALL	ET
		MOVLW	b'10101'
		MOVWF	PORTA
		CALL	ET
		RETURN

Letter_A	
		MOVLW	b'10100'
		MOVWF	PORTA
		CALL	ET
		MOVLW	b'10001'
		MOVWF	PORTA
		CALL	ET
		RETURN

;TOGGLE
ET		BSF		PORTB,1
		NOP
		BCF		PORTB,1
		Call	DELAY
		RETURN


SET_AST
		;Set Address
		MOVF	tempA1,w
		MOVWF 	PORTA
		CALL  	ET
		MOVF	tempA2,w
		MOVWF 	PORTA
		CALL  	ET

		CALL 	Blank_Space

		;Set Address
		MOVF	tempB1,w
		MOVWF 	PORTA
		CALL 	ET
		MOVF	tempB2,w
		MOVWF 	PORTA
		CALL 	ET
		
		CALL 	Asterisk
		RETURN	


BOX		MOVLW	b'11101'
		MOVWF 	PORTA
		CALL 	ET
		MOVLW	b'11011'
		MOVWF 	PORTA
		CALL 	ET	
		RETURN


INITIAL_CARDS
		;DISPLAY ON/OFF (activating cursor)
		MOVLW	b'00000'
		MOVWF	PORTA
		CALL	ET
		MOVLW	b'01110'
		MOVWF	PORTA
		CALL	ET

		CALL	Clear_Display
		
		
		;Set Address
		CALL	Clear_Address

		CALL	UTILITY
		
		;Set Address
		MOVLW	b'01100'
		MOVWF 	PORTA
		CALL 	ET
		MOVLW	b'00000'
		MOVWF 	PORTA
		CALL 	ET
		
		CALL	UTILITY

		RETURN

UTILITY	MOVLW	d'6'
		MOVWF	COUNTER
L		CALL	BOX
		DECFSZ	COUNTER
		GOTO	L
		RETURN


PLAY_GAME
		BTFSS	PORTB,5
		GOTO	RIGHT
		BTFSS	PORTB,4
		GOTO	LEFT
		BTFSS	PORTB,6
		GOTO	UP_DOWN
		BTFSS	PORTB,7
		GOTO	FLIP
		GOTO	DONE

RIGHT	MOVF	AC,w
		MOVWF	c1
		BCF		c1,7
		MOVLW	d'5'
		SUBWF	c1
		BTFSC	STATUS,Z
		GOTO	BUZZ
		MOVLW	b'01000'
		BTFSC	AC,7
		ADDLW	b'100'
		MOVWF	PORTA
		CALL	ET
		INCF	AC
		MOVF	AC,w
		MOVWF	PORTA
		CALL	ET
		GOTO	DONE

LEFT	MOVF	AC,w
		MOVWF	c1
		BCF		c1,7
		CLRW	
		SUBWF	c1
		BTFSC	STATUS,Z
		GOTO	BUZZ
		MOVLW	b'01000'
		BTFSC	AC,7
		ADDLW	b'100'
		MOVWF	PORTA
		CALL	ET
		DECF	AC
		MOVF	AC,w
		MOVWF	PORTA
		CALL	ET
		GOTO 	DONE


UP_DOWN	MOVLW	b'01000'
		MOVWF	PORTA
		BTFSS	AC,7
		BSF		PORTA,2
		BTFSC	AC,7
		BCF		PORTA,2
		CALL	ET

		MOVLW	b'10000000'
		XORWF	AC
		MOVF	AC,w
		MOVWF	PORTA
		CALL	ET
		GOTO	DONE

FLIP	MOVLW	d'35'
		MOVWF	FSR
		MOVF	AC,w
		MOVWF	c1
		MOVLW	d'6'
		BTFSC	c1,7
		ADDWF	c1
		BCF		c1,7
		MOVF	c1,w
		ADDWF	FSR
		BTFSC	INDF,0
		GOTO	BUZZ
		
		
		BTFSS	IsOpen,0
		GOTO	FIRST
		GOTO	SECOND

FIRST	MOVF	AC,w
		MOVWF	RECENT@
		MOVLW	d'12'
		SUBWF	FSR
		MOVLW	b'10100'
		MOVWF	PORTA
		CALL	ET
		MOVF	INDF,w
		MOVWF	RECENT
		IORLW	b'10000'
		MOVWF	PORTA
		CALL	ET
		
		CALL	Clear_Address
		CLRF	AC
		
		MOVLW	d'12'
		ADDWF	FSR
		MOVF	FSR,w
		MOVWF	TEMP
		BSF		INDF,0
		BSF		IsOpen,0
		GOTO	DONE
		
SECOND	BCF		IsOpen,0
		MOVLW	d'12'
		SUBWF	FSR
		MOVLW	b'10100'
		MOVWF	PORTA
		CALL	ET
		MOVF	INDF,w
		IORLW	b'10000'
		MOVWF	PORTA
		CALL	ET
		
		MOVF	RECENT,w
		SUBWF	INDF,0
		BTFSS	STATUS,Z
		GOTO	MISMATCH

MATCH	INCF	MATCHES
		MOVLW	d'12'
		ADDWF	FSR
		BSF		INDF,0
		
		CALL	Clear_Address
		CLRF	AC
		CLRF	RECENT
		GOTO	BUZZ
		
MISMATCH
		INCF	NOMATCH
		MOVF	TEMP,w
		MOVWF	FSR
		BCF		INDF,0
		BSF		PORTB,2
		CALL	DELAY4
	
		MOVLW	b'01000'
		MOVWF	PORTA
		MOVLW	b'100'
		BTFSC	AC,7
		ADDWF	PORTA
		CALL	ET
		MOVF	AC,w
		MOVWF	PORTA
		CALL	ET

		CALL	BOX	
		
		MOVLW	b'01000'
		MOVWF	PORTA
		MOVLW	b'100'		
		BTFSC	RECENT@,7
		ADDWF	PORTA
		CALL	ET
		MOVF	RECENT@,w
		MOVWF	PORTA
		CALL	ET
		
		CALL	BOX
		
		MOVLW	d'35'
		MOVWF	FSR
		MOVF	AC,w
		MOVWF	c1
		MOVLW	d'6'
		BTFSC	c1,7
		ADDWF	c1
		BCF		c1,7
		MOVF	c1,w
		ADDWF	FSR
		BCF		INDF,0
		
		CALL	Clear_Address
		CLRF	AC
		BCF		PORTB,2
		GOTO	DONE

BUZZ	BSF		PORTB,0
		CALL	DELAY3
		BCF		PORTB,0

DONE	RETURN


Clear_Address
		MOVLW	b'01000'
		MOVWF	PORTA
		CALL	ET
		MOVLW	b'00000'
		MOVWF	PORTA
		CALL	ET
		RETURN
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; DELAYS ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;1.5 ms delay
DELAY	MOVLW	h'00'
		MOVWF	COUNT1
		MOVLW	d'2'
		MOVWF	COUNT2
LOOP	INCFSZ	COUNT1,F
		GOTO	LOOP
		DECFSZ	COUNT2,F
		GOTO 	LOOP
		RETURN

;40 MS DELAY
DELAY2	MOVLW	h'00'
		MOVWF	COUNT1
		MOVLW	d'52'
		MOVWF	COUNT2
LOOP2	INCFSZ	COUNT1,F
		GOTO	LOOP2
		DECFSZ	COUNT2,F
		GOTO 	LOOP2
		RETURN

DELAY3	MOVLW	h'00'
		MOVWF	COUNT1
		MOVLW	d'255'
		MOVWF	COUNT2
LOOP3	INCFSZ	COUNT1,F
		GOTO	LOOP3
		DECFSZ	COUNT2,F
		GOTO 	LOOP3
		RETURN

DELAY4	MOVLW	h'00'
		MOVWF	COUNT1
		MOVLW	d'255'
		MOVWF	COUNT2
		MOVLW	d'10'
		MOVWF	COUNTER
LOOP4	INCFSZ	COUNT1,F
		GOTO	LOOP4
		DECFSZ	COUNT2,F
		GOTO 	LOOP4
		DECFSZ	COUNTER,F
		GOTO 	LOOP4
		RETURN


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;; INTERRUPTS ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

RBINT	CALL	DELAY
		BTFSC	SCENE,3
		GOTO	MENUINT
		BTFSC	SCENE,2
		GOTO	MODE1INT
		BTFSC	SCENE,1
		GOTO	MODE2INT
		BTFSC	SCENE,0
		GOTO	MODE3INT

MENUINT	BTFSS	PORTB,7
		GOTO	CHECK_MODE
		BTFSS	PORTB,5
		GOTO	MOVE_AST
		GOTO	LEAVEINT1

CHECK_MODE
		MOVLW	d'4'
		MOVWF	SCENE
		MOVF 	AC,w
		MOVWF 	c1
		MOVLW 	d'6'
		SUBWF 	c1
		BTFSC 	STATUS,Z
		GOTO 	LEAVEINT1

  		MOVLW	d'2'
		MOVWF	SCENE
		MOVF 	AC,w
		MOVWF	c1
		MOVLW	d'9'
		SUBWF	c1
		BTFSC	STATUS,Z
		GOTO 	LEAVEINT1

  		MOVLW	d'1'
		MOVWF	SCENE
		GOTO 	LEAVEINT1
		
MOVE_AST	
		MOVF	AC,w
		MOVWF	c1
		MOVLW 	d'12'
		SUBWF	c1
		BTFSC	STATUS,Z
		GOTO	AT_3

		MOVF	AC,w
		MOVWF	c1
		MOVLW	d'9'
		SUBWF	c1
		BTFSC	STATUS,Z
		GOTO	AT_2
  
		MOVF	AC,w
		MOVWF	c1
		MOVLW	d'6'
		SUBWF	c1
		BTFSC	STATUS,Z
		GOTO	AT_1
		GOTO	LEAVEINT1


AT_3	MOVLW	d'6'
		MOVWF	AC
		MOVLW	b'01000'
		MOVWF	tempA1
  		MOVLW	b'01011'
		MOVWF	tempA2
		MOVLW	b'01000'
		MOVWF	tempB1
		MOVLW	b'00101'
		MOVWF	tempB2
		CALL	SET_AST

		GOTO	LEAVEINT1

AT_2	MOVLW	d'12'
		MOVWF	AC
		MOVLW	b'01000'
		MOVWF	tempA1
  		MOVLW	b'01000'
		MOVWF	tempA2
		MOVLW	b'01000'
		MOVWF	tempB1
		MOVLW	b'01011'
		MOVWF	tempB2
		CALL	SET_AST
  
		GOTO	LEAVEINT1
  
AT_1	MOVLW	d'9'
		MOVWF	AC
 		MOVLW	b'01000'
		MOVWF	tempA1
  		MOVLW	b'00101'
		MOVWF	tempA2
		MOVLW	b'01000'
		MOVWF	tempB1
		MOVLW	b'01000'
		MOVWF	tempB2
		CALL	SET_AST
		
		GOTO 	LEAVEINT1
  
MODE1INT
		CALL	PLAY_GAME			
		MOVLW	d'6'
		SUBWF	MATCHES,0
		BTFSC	STATUS,Z
		GOTO	END1

		MOVLW	d'9'
		SUBWF	tempA1,0
		BTFSC	STATUS,Z
		GOTO	FA
		MOVLW	d'2'
		SUBWF	NOMATCH
		BTFSC	STATUS,Z
		GOTO	WEAKER
		ADDWF	NOMATCH
		GOTO	LEAVEINT1
FA		DECF	NOMATCH,0
		BTFSC	STATUS,Z
		GOTO	WEAKER
		GOTO	LEAVEINT1

WEAKER	MOVLW	b'01000'
		MOVWF	PORTA
		CALL	ET
		MOVF	tempA1,w
		MOVWF	PORTA
		CALL	ET

		MOVLW	b'11111'
		MOVWF	PORTA
		CALL	ET
		MOVLW	b'11111'
		MOVWF	PORTA
		CALL	ET

		CALL	Clear_Address
		INCF	tempA1
		CLRF	NOMATCH
		CLRF	AC
		GOTO 	LEAVEINT1

END1	MOVLW	b'01100'
		MOVWF	PORTA
		CALL	ET
		MOVLW	b'01001'
		MOVWF	PORTA
		CALL	ET
		
		MOVLW	b'1000'
		MOVWF	SCENE
		
		MOVLW	d'11'
		SUBWF	tempA1,0
		MOVWF	tempB1
		BTFSC	tempB1,7
		GOTO	SUPER
		MOVLW	d'13'
		SUBWF	tempA1,0
		MOVWF	tempB1
		BTFSC	tempB1,7
		GOTO	AVG
		MOVLW	d'15'
		SUBWF	tempA1,0
		MOVWF	tempB1
		BTFSC	tempB1,7
		GOTO	WEAK

SUPER	CALL	Letter_S

		MOVLW	b'10101'
		MOVWF	PORTA
		CALL	ET
		MOVLW	b'10101'
		MOVWF	PORTA
		CALL	ET

		MOVLW	b'10101'
		MOVWF	PORTA
		CALL	ET
		MOVLW	b'10000'
		MOVWF	PORTA
		CALL	ET

		CALL	Letter_E
		CALL	Letter_R
		
		BSF		PORTB,3
		CALL	DELAY4
		BSF		PORTB,2
		BCF		PORTB,3
		CALL	DELAY4
		BSF		PORTB,3
		BCF		PORTB,2
		CALL	DELAY4
		BCF		PORTB,3
		GOTO	LEAVEINT1

AVG		CALL	Letter_A

		MOVLW	b'10101'
		MOVWF	PORTA
		CALL	ET
		MOVLW	b'10110'
		MOVWF	PORTA
		CALL	ET

		MOVLW	b'10100'
		MOVWF	PORTA
		CALL	ET
		MOVLW	b'10111'
		MOVWF	PORTA
		CALL	ET
		BSF		PORTB,3
		CALL	DELAY4
		BCF		PORTB,3
		GOTO	LEAVEINT1

WEAK	CALL	Letter_W
		CALL	Letter_E
		CALL	Letter_A
		
		MOVLW	b'10100'
		MOVWF	PORTA
		CALL	ET
		MOVLW	b'11011'
		MOVWF	PORTA
		CALL	ET
		BSF		PORTB,2
		CALL	DELAY4
		BCF		PORTB,2
		GOTO	LEAVEINT1
MODE2INT
MODE3INT

LEAVEINT1	BCF	INTCON,RBIF
			RETFIE
	
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;	
end
